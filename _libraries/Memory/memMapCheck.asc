;; ==[ CALL MemMapCheck ]===============================================
;; Bepaalt de grootte van de memorymapper in paginas en welke pagina in page 3 geladen is. 
;;
;; Gebruikt label:
;; - MEMBUFFER (34) : Tijdelijk 34 byte buffer voor het controleren. 
;;
;; OUTPUT:
;; - MEMNRBANKS (1) : Bevat het aantal memoryslots (max 32 = 512kb)
;; - MEMPAGE3   (1) : Bevat de memory page dat voor page 3 gerbuikt wordt. (Deze niet veranderen!)
;; - A :  Bevat het aantal memoryslots
;;
;; Werking:
;; strategie: we wisselen slot 3. 
;; dan schrijven we een waarde in $8000 
;; max 32 switches, dus max 32*16 = 512kB memorymapper.

MemMapCheck:

    ld A, ($8000)           ; bewaar de 2 bytes van huidige slot 2 en slot 3 
    ld (MEMBUFFER+32), A    ; zodat we deze kunnen terugzetten als we klaar zijn
    ld A, ($C000) 
    ld (MEMBUFFER+33), A  

    ld A, $FF
    ld (MEMPAGE3), A    ; markeer invalid.

    ld A, $C0           ; markeer deze page 3
    ld ($C000), A

    ld b, 32            ; we gaan de memorypages achterstevoren doorlopen.

MEMBUF_LOOP:    
    ld a, b             ; switch naar mem-bank b
    dec a               
    out ($FE), a

    ld A, ($8000)       ; lees huidige waarde in slot 2

    cp $C0              ; is deze al gemarkeerd?
    jp z, MEMBUF_isC0

    ld a, b             ; schrijf b (als het niet de huidige bank is)
    ld ($8000), a       ; en in slot 2

MEMBUF_CONT:    
    djnz MEMBUF_LOOP    ; verlaag b en herhaal tot we alle 32 banks hebben getest

    ld HL, MEMBUFFER+31
    ld b, 32

MEMBUF_LOOP2:
    ld a, b             ; switch naar mem-bank b
    dec a
    out ($FE), a

    ld A, ($8000)       ; lees huidige waarde in slot 2 
    ld (HL), A          ; bewaar deze waarde in MEMBUFFER

    dec hl
    djnz MEMBUF_LOOP2   ; verlaag b en herhaal tot we alle 32 banks hebben getest

    ld A, (MEMBUFFER+32) ; cleanup
    ld ($8000), A  
    ld A, (MEMBUFFER+33)
    ld ($C000), A  

                        ; ** Nu het maximum bepalen.**
    ld hl, MEMBUFFER    ; Startadres van de data
    ld b, 32            ; 32 bytes (de page3-mem-bank later checken)
    ld c, 0             ; Hierin bewaren we de maximale waarde

MEMBUF_MaxLoop:
    ld a, (hl)          ; Lees byte uit geheugen
    
    cp $C0              ; Overslaan als waarde C0h is
    jr z, MEMBUF_Next
    
    cp c                ; Vergelijk huidige byte (A) met maximum (C)
    jr c, MEMBUF_Next   ; Als A < C, dan is C nog steeds de grootste
    
    ld c, a             ; A is groter of gelijk aan C, dus C = A

MEMBUF_Next:
    inc hl
    djnz MEMBUF_MaxLoop ; Volgende van de 32 bytes

    ld a, c
    ld (MEMNRBANKS), a  ; Sla de maximale waarde op in MAX
    ret

MEMBUF_isC0:
    ld a, b
    dec a
    ld (MEMPAGE3), a
    jp MEMBUF_CONT
