;; ==[ CALL GetNextCluster ]===============================================
;; Vindt het voglende clusternummer in de FAT
;; FAT1 is geladen in DISKBUFFER
;;
;; INPUT:
;; - DE:                Huidig clusternummer
;; - DISKBUF: ($600)    Adres waar de FAT1 is opgeslagen. (3 Ã— 512 = 1536 bytes) (3e sector alleen bij 720Kb)
;;
;; OUTPUT:
;; - DE:                Volgend clusternummer 

GetNextCluster:
    push de             ; Bewaar DE (huidig cluster)

    ld h, d             ; Bereken Offset: HL = DE * 1.5
    ld l, e             ; HL = cluster
    srl h
    rr l                ; HL = cluster / 2
    add hl, de          ; HL = cluster + (cluster / 2) = cluster * 1.5

    ld de, DISKBUF      ; Voeg startadres van FAT-buffer toe
    add hl, de

    ld e, (hl)          ; Laad 16-bit word uit de FAT
    inc hl
    ld d, (hl)          ; DE = de 2 bytes waar het 12-bit nummer in zit

    pop hl              ; Herstel HL (vanuit DE->HL) (huidig cluster)
    ld a, l             ; Bepaal of cluster EVEN of ONEVEN was (check bit 0 van L)
    rra                 ; Schuif bit 0 in de Carry vlag
    jr nc, GNC_Even

GNC_Odd:
    rept 4              ; Als cluster ONEVEN is: DE = DE >> 4 (neem bovenste 12 bits)
    srl d               ; Herhaal 4 keer 
    rr e
    endr
    ret

GNC_Even:
    ld a, d             ; Als cluster EVEN is: DE = DE AND 0FFFh (neem onderste 12 bits)
    and $0F             ; Masker de bovenste 4 bits weg
    ld d, a
    ret