;; ==[ CALL GetFirstKey ]===============================================
;; Checks and reads for a key without bios
;;
;; OUTPUT
;; - A: the key (see table below), or 0 if no key was pressed.
;;
;; TIP:  maak een kleine letter van een hoofdletter: set 5, a
;;       Different countries have different layouts :( this uses the international layout.
;;       For text input, maybe the bios version is better..

GetFirstKey:
    ld b, 9             ; dont do keypad (for now)
    di
GetFirstKeyLoop:
    ld a, b
    dec a
    out ($AA), a        ; Schrijf rijnummer naar PPI poort C
    in a, ($A9)         ; Lees de 8 toetsen van poort A
    cpl
    or a
    jr nz, GetFirstKeyfound

    djnz GetFirstKeyLoop
    ld a, 0
    ei
    ret
GetFirstKeyfound:       ; a = rowbits
    ld c, b             ; c = rownumber
    dec c
    ld b, 8             ; loop de bits van a
GetFirstKeyBitloop:
    rlca
    jr c, GetFirstKeyBitFound
    djnz GetFirstKeyBitloop
    ld a, 0
    ei
    ret
GetFirstKeyBitFound:
    dec b
    ; b = nu de bitindex (0-7), c = de rij (0-8)
    ld a, c       ; A = C
    add a, a      ; A = C * 2
    add a, a      ; A = C * 4
    add a, a      ; A = C * 8
    add a, b      ; A = (C * 8) + B a is nu de index in de tabel as 1d-array

    ld c, a
    ld b, 0
    ld hl, GetFirstKeybitlist
    add hl, bc

    ld a, (HL)  ; geconverteerd naar een ascii teken (de speciale zijn speciaal hah)
    ei 
    ret

GetFIrstKeybitlist:
; De MSX Toetsenmatrix 
; ====================
; Rij	Bit 7	Bit 6	Bit 5	Bit 4	Bit 3	Bit 2	Bit 1	Bit 0
; 0	    7	    6	    5	    4	    3	    2	    1	    0
; 1	    ;	    ]	    [	    \	    =	    -	    9	    8
; 2	    B	    A	    _	    /	    .	    ,	    `	    '
; 3	    J	    I	    H	    G	    F	    E	    D	    C
; 4	    R	    Q	    P	    O	    N	    M	    L	    K
; 5	    Z	    Y	    X	    W	    V	    U	    T	    S
; 6	    F3	    F2	    F1	    CODE	CAP	    GRAPH	CTRL	SHIFT
; 7	    RET	    SELECT	BS	    STOP	TAB	    ESC	    F5	    F4
; 8	    →	    ↓	    ↑	    ←	    DEL	    INS 	HOME	SPACE
;
; 9	    NUM 4	NUM 3	NUM 2	NUM 1	NUM 0	NUM /	NUM +	NUM *
; 10	NUM .	NUM ,	NUM -	NUM 9	NUM 8	NUM 7	NUM 6	NUM 5

    db '0', '1', '2', '3', '4', '5', '6', '7'
    db '8', '9', '-', '=', '\', '[', ']', $3B       ; $3B = puntkomma
    db $27, '`', ',', '.', '/', '_', 'A', 'B'       ; $27 = quote
    db 'C', 'D', 'E', 'F', 'G', 'H', 'I', 'J'
    db 'K', 'L', 'M', 'N', 'O', 'P', 'Q', 'R'
    db 'S', 'T', 'U', 'V', 'W', 'X', 'Y', 'Z'
    db 's', 'c', 'g', 'a', 'o', '!', '@', '#' ; s shift, c ctrl, g graph, a caps, o code, !@# = F1 F2 F3
; international
   db '$', '%', 'e', 'z', 't', 'b', 'q', 'n' ; $ F4, % F5, e escape, z tab, t stop, b BS, q select, n return
   db ' ', 'h', 'p', 'x', 'l', 'u', 'd', 'r' ; r Right, l left, d down, u up, x del, p ins, h home, ' ' space

 

